plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

archivesBaseName = "$group-$name"
description = 'TripleA Lobby'
mainClassName = 'org.triplea.lobby.server.LobbyRunner'

dependencies {
    implementation project(':game-core')
    implementation 'org.mindrot:jbcrypt:0.4'
    implementation 'com.sparkjava:spark-core:2.8.0'

    runtimeOnly 'org.slf4j:slf4j-simple:1.7.21'
    runtimeOnly "org.postgresql:postgresql:$postgresqlVersion"

    testImplementation project(':test-common')
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task lobbyArchive(type: Zip, group: 'release', dependsOn: shadowJar) {
    from('config') {
        into 'config'
    }
    from(shadowJar.outputs) {
        into 'bin'
    }
}

task release(group: 'release', dependsOn: lobbyArchive) {
    doLast {
        publishArtifacts(lobbyArchive.outputs.files)
    }
}
