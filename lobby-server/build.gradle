plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

archivesBaseName = "$group-$name"
description = 'TripleA DropWizard Http Server'
mainClassName = 'org.triplea.http.ServerApplication'
ext {
    releasesDir = file("$buildDir/releases")
}


configurations {
    testImplementation {
        // database-rider brings in slf4j-simple as a transitive dependency
        // DropWizard has logback baked in and cannot have multiple slf4j bindings.
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
}

dependencies {
    errorprone "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"

    implementation "com.liveperson:dropwizard-websockets:$dropwizardWebsocketsVersion"
    implementation "com.sun.mail:jakarta.mail:$jakartaMailVersion"
    implementation "com.sun.xml.bind:jaxb-core:$jaxbCoreVersion"
    implementation "com.sun.xml.bind:jaxb-impl:$jaxbImplVersion"
    implementation "es.moki.ratelimitj:ratelimitj-dropwizard:$rateLimitjVersion"
    implementation "es.moki.ratelimitj:ratelimitj-inmemory:$rateLimitjVersion"
    implementation "io.dropwizard:dropwizard-auth:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-core:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-jdbi3:$dropwizardVersion"
    implementation "io.github.openfeign:feign-gson:$openFeignVersion"
    implementation "javax.activation:activation:$javaxActivationVersion"
    implementation "javax.xml.bind:jaxb-api:$jaxbApiVersion"
    implementation "org.jdbi:jdbi3-core:$jdbiVersion"
    implementation "org.jdbi:jdbi3-sqlobject:$jdbiVersion"
    implementation "at.favre.lib:bcrypt:$bcryptVersion"
    implementation project(':domain-data')
    implementation project(':dropwizard-common')
    implementation project(':http-clients')
    implementation project(':java-extras')
    runtimeOnly "org.postgresql:postgresql:$postgresqlVersion"
    testImplementation "com.github.database-rider:rider-junit5:$databaseRiderVersion"
    testImplementation "com.sun.mail:jakarta.mail:$jakartaMailVersion"
    testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
    testImplementation "org.awaitility:awaitility:$awaitilityVersion"
    testImplementation "org.java-websocket:Java-WebSocket:$javaWebsocketVersion"
    testImplementation "uk.co.datumedge:hamcrest-json:$hamcrestJsonVersion"
    testImplementation project(":dropwizard-common-test")
    testImplementation project(":test-common")
    testImplementation project(':domain-data')
    testImplementation project(':http-clients')
    testImplementation project(':java-extras')
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task portableInstaller(type: Zip, group: 'release', dependsOn: shadowJar) {
    from file('configuration-prerelease.yml')
    from file('configuration-production.yml')

    from(shadowJar.outputs) {
        into 'bin'
    }
}

task release(group: 'release', dependsOn: portableInstaller) {
    doLast {
        publishArtifacts(portableInstaller.outputs.files)
    }
}

shadowJar {
    archiveClassifier.set ''
}
