import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "application"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

application {
    mainClass = "org.triplea.game.server.HeadlessGameRunner"
}

dependencies {
    implementation project(":http-clients:lobby-client")
    implementation project(":game-app:ai")
    implementation project(":game-app:domain-data")
    implementation project(":game-app:game-core")
    implementation project(":lib:java-extras")
}

jar {
    archiveBaseName = "$group-$name"
    manifest {
        attributes "Main-Class": application.mainClass
    }
}

run {
    systemProperty "triplea.lobby.game.comments", "automated_host"
    systemProperty "triplea.lobby.uri", "http://127.0.0.1:3000"
    systemProperty "triplea.name", "Bot_Local01"
    systemProperty "triplea.port", "3300"
    systemProperty "triplea.server", "true"
}

def portableInstaller = tasks.register("portableInstaller", Zip) {
    group = "release"

    from file(".triplea-root")
    from file("scripts/run_bot")
    from(file("scripts/run_bot.bat")) {
        filter ReplaceTokens, tokens: [version: project.version]
        filter FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance("crlf") // workaround for https://github.com/gradle/gradle/issues/1151
    }
    from(shadowJar) {
        into "bin"
    }
}

tasks.register("release", Copy) {
    group = "release"

    from portableInstaller
    into file(project.layout.buildDirectory.dir("artifacts"))

    doFirst {
        inputs.files.forEach {
            if (!it.exists()) {
                throw new GradleException("artifact '$it' does not exist")
            }
        }
    }
}

tasks.register("copyShadow", Copy) {
    from shadowJar
    into "../infrastructure/ansible/roles/bot/files/"
}

shadowJar {
    archiveClassifier.set ""
}
