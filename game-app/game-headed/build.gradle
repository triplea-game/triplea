plugins {
    id "application"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.install4j.gradle" version "12.0"
    id "de.undercouch.download" version "5.6.0"
}

application {
    applicationDefaultJvmArgs = [
            //This flag fixes touch gestures in Java-21ï¼›Technical Reference: https://stackoverflow.com/questions/48535595/what-replaces-gestureutilities-in-java-9
            "--add-opens=java.desktop/com.apple.eawt.event=ALL-UNNAMED"
    ]
}

archivesBaseName = "$group-$name"
mainClassName = "org.triplea.game.client.HeadedGameRunner"

ext {
    releasesDir = file("$buildDir/releases")
    releaseVersion = getProductVersion() + "+" + getCommitNumber()
}

dependencies {
    implementation project(":game-app:ai")
    implementation project(":game-app:domain-data")
    implementation project(":game-app:game-core")
    implementation project(":game-app:map-data")
    implementation project(":http-clients:lobby-client")
    implementation project(":lib:feign-common")
    implementation project(":lib:java-extras")
    implementation project(":lib:swing-lib")
    implementation project(":lib:websocket-client")
    testImplementation project(":lib:test-common")
}

jar {
    manifest {
        attributes "Main-Class": mainClassName
    }
}

def assetsUrl = "https://github.com/triplea-game/assets/releases/download/47/game_headed_assets.zip"
def assetsZipFile = file("$projectDir/.assets/assets.zip")
def assetsCacheDir = file("$projectDir/.assets")
def assetsUnpackedDir = file("$projectDir/assets")

clean.doFirst {
    delete assetsCacheDir
    delete assetsUnpackedDir
}

 
task downloadAssets(type: Download) {
    src assetsUrl
    dest assetsZipFile
    overwrite false
    // declare outputs for up-to-date checks (Download task normally already behaves well)
    outputs.file assetsZipFile
}

task unpackAssets(type: Copy) {
    dependsOn downloadAssets
    from { zipTree(assetsZipFile) }
    into assetsUnpackedDir
    inputs.file assetsZipFile
    outputs.dir assetsUnpackedDir

    // Only run the unpack if the download task actually wrote the file:
    onlyIf { downloadAssets.didWork || !assetsUnpackedDir.exists() }
}

run {
    dependsOn unpackAssets
}

runShadow {
    dependsOn unpackAssets
}

task platformInstallers(
        type: com.install4j.gradle.Install4jTask,
        group: "release",
        dependsOn: [shadowJar]) {
    projectFile = file("build.install4j")
    release = releaseVersion
    license = "$System.env.INSTALL4J_LICENSE_KEY"
    doLast {
        ant.chmod(dir: releasesDir, perm: "+x", includes: "*.sh")
    }
}

task portableInstaller(type: Zip, group: "release", dependsOn: shadowJar) {
    from file(".triplea-root")
    from(file("assets")) {
        into "assets"
    }
    from(file("dice_servers")) {
        into "dice_servers"
    }
    from(shadowJar.outputs) {
        into "bin"
    }
}

// creates installer files using install4j (eg: install.exe)
task release(group: "release", dependsOn: [portableInstaller, platformInstallers, downloadAssets]) {
    doLast {
        publishArtifacts(portableInstaller.outputs.files + [
                file("$releasesDir/TripleA_${releaseVersion}_macos.dmg"),
                file("$releasesDir/TripleA_${releaseVersion}_unix.sh"),
                file("$releasesDir/TripleA_${releaseVersion}_windows-64bit.exe")
        ])
    }
}

shadowJar {
    // "archiveVersion" sets the version number on packaged jar files
    // eg: "2.6+105234" in "lobby-server-2.6+50370c.jar"
    archiveVersion = releaseVersion
    archiveClassifier.set ""
}
