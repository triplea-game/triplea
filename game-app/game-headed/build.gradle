plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.install4j.gradle' version '10.0.6'
    id 'de.undercouch.download' version '5.5.0'
}

archivesBaseName = "$group-$name"
mainClassName = 'org.triplea.game.client.HeadedGameRunner'

ext {
    releasesDir = file("$buildDir/releases")
    releaseVersion = getProductVersion() + "+" + getCommitNumber()
}

dependencies {
    implementation "io.github.openfeign:feign-gson:$openFeignVersion"
    implementation "org.pushing-pixels:radiance-substance:$substanceVersion"
    implementation project(":game-app:ai")
    implementation project(":game-app:domain-data")
    implementation project(":game-app:game-core")
    implementation project(":game-app:map-data")
    implementation project(":http-clients:lobby-client")
    implementation project(":lib:feign-common")
    implementation project(":lib:http-client-lib")
    implementation project(":lib:java-extras")
    implementation project(":lib:swing-lib")
    implementation project(":lib:websocket-client")
    implementation project(":servers:maps:client")
    testImplementation "org.sonatype.goodies:goodies-prefs:$sonatypeGoodiesPrefsVersion"
    testImplementation project(":lib:test-common")
}

install4j {
    // If you wish to build the installers, you must install install4j and define the "install4jHomeDir" property on the
    // command line (e.g. -Pinstall4jHomeDir=...) or in your personal Gradle properties (e.g. ~/.gradle/gradle.properties).
    installDir = file(project.findProperty('install4jHomeDir') ?: '.')
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

clean.doFirst {
  delete "$projectDir/.assets/"
  delete "$projectDir/assets/"
}

task downloadAssets {
    doLast {
        download.run {
            src "https://github.com/triplea-game/assets/releases/download/47/game_headed_assets.zip"
            dest "$projectDir/.assets/assets.zip"
            overwrite false
        }
        copy {
            from zipTree(file("$projectDir/.assets/assets.zip"))
            into "$projectDir/assets/"
        }
    }
}


run {
    dependsOn downloadAssets
    dependsOn rootProject.composeUp
    dependsOn(":spitfire-server:database:flywayMigrateLobbyDb")
    dependsOn(":spitfire-server:database:flywayMigrateErrorReportDb")
}

runShadow {
    dependsOn downloadAssets
}

// Downloads JDK bundles that are used to create installers
task downloadPlatformInstallerAssets(group: 'release', dependsOn: downloadAssets) {
    doLast {
        [
            'install4j/OpenJDK11U-jre_x86-32_windows_hotspot_11.0.4_11.tar.gz',
        ].each { path ->
            download.run {
                src "https://raw.githubusercontent.com/triplea-game/assets/master/$path"
                dest "$buildDir/assets/$path"
                overwrite false
            }
        }
    }
}

task platformInstallers(
    type: com.install4j.gradle.Install4jTask,
    group: 'release',
    dependsOn: [shadowJar, downloadPlatformInstallerAssets]) {
    projectFile = file('build.install4j')
    release = releaseVersion

    doLast {
        ant.chmod(dir: releasesDir, perm: '+x', includes: '*.sh')
    }
}

task portableInstaller(type: Zip, group: 'release', dependsOn: shadowJar) {
    from file('.triplea-root')
    from(file('assets')) {
        into 'assets'
    }
    from(file('dice_servers')) {
        into 'dice_servers'
    }
    from(shadowJar.outputs) {
        into 'bin'
    }
}

// creates installer files using install4j (eg: install.exe)
task release(group: 'release', dependsOn: [portableInstaller, platformInstallers]) {
    doLast {
        publishArtifacts(portableInstaller.outputs.files + [
            file("$releasesDir/TripleA_${releaseVersion}_macos.dmg"),
            file("$releasesDir/TripleA_${releaseVersion}_unix.sh"),
            file("$releasesDir/TripleA_${releaseVersion}_windows-32bit.exe"),
            file("$releasesDir/TripleA_${releaseVersion}_windows-64bit.exe")
        ])
    }
}

shadowJar {
    // 'archiveVersion' sets the version number on packaged jar files
    // eg: "2.6+105234" in "lobby-server-2.6+50370c.jar"
    archiveVersion = releaseVersion
    archiveClassifier.set ''
}
