import com.install4j.gradle.Install4jTask

plugins {
    id "application"
    id "com.install4j.gradle" version "12.0.1"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "de.undercouch.download" version "5.6.0"
}

application {
    mainClass = "org.triplea.game.client.HeadedGameRunner"
    applicationDefaultJvmArgs = [
            //This flag fixes touch gestures in Java-21ï¼›Technical Reference: https://stackoverflow.com/questions/48535595/what-replaces-gestureutilities-in-java-9
            "--add-opens=java.desktop/com.apple.eawt.event=ALL-UNNAMED"
    ]
}

def releasesDir = file("$buildDir/releases")
def releaseVersion = getProductVersion() + "+" + getCommitNumber()

dependencies {
    implementation project(":game-app:ai")
    implementation project(":game-app:domain-data")
    implementation project(":game-app:game-core")
    implementation project(":game-app:map-data")
    implementation project(":http-clients:lobby-client")
    implementation project(":lib:feign-common")
    implementation project(":lib:java-extras")
    implementation project(":lib:swing-lib")
    implementation project(":lib:websocket-client")
    testImplementation project(":lib:test-common")
}

jar {
    archiveBaseName = "$group-$name"
    manifest {
        attributes "Main-Class": application.mainClass
    }
}

clean.doFirst {
  delete "$projectDir/.assets/"
  delete "$projectDir/assets/"
}

def downloadAssets = tasks.register("downloadAssets") {
    doLast {
        download.run {
            src "https://github.com/triplea-game/assets/releases/download/47/game_headed_assets.zip"
            dest "$projectDir/.assets/assets.zip"
            overwrite false
        }
        copy {
            from zipTree(file("$projectDir/.assets/assets.zip"))
            into "$projectDir/assets/"
        }
    }
}


run {
    dependsOn downloadAssets
}

runShadow {
    dependsOn downloadAssets
}

def platformInstallers = tasks.register("platformInstallers", Install4jTask) {
    group = "release"
    description = "creates installer files using install4j (eg: install.exe)"
    dependsOn(shadowJar)

    projectFile = file("build.install4j")
    release = releaseVersion
    license="$System.env.INSTALL4J_LICENSE_KEY"
    doLast {
        ant.chmod(dir: releasesDir, perm: "+x", includes: "*.sh")
    }
}

def portableInstaller = tasks.register("portableInstaller", Zip) {
    group = "release"

    from file(".triplea-root")
    from(file("assets")) {
        into "assets"
    }
    from(file("dice_servers")) {
        into "dice_servers"
    }
    from(shadowJar) {
        into "bin"
    }
}

tasks.register("release", Copy) {
    group = "release"
    dependsOn(downloadAssets, platformInstallers)

    from portableInstaller
    from file("$releasesDir/TripleA_${releaseVersion}_macos.dmg")
    from file("$releasesDir/TripleA_${releaseVersion}_unix.sh")
    from file("$releasesDir/TripleA_${releaseVersion}_windows-64bit.exe")
    into file(project.layout.buildDirectory.dir("artifacts"))

    doFirst {
        inputs.files.forEach {
            if (!it.exists()) {
                throw new GradleException("artifact '$it' does not exist")
            }
        }
    }
}

shadowJar {
    // "archiveVersion" sets the version number on packaged jar files
    // eg: "2.6+105234" in "lobby-server-2.6+50370c.jar"
    archiveVersion = releaseVersion
    archiveClassifier.set ""
}
