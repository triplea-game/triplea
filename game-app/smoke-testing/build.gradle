plugins {
    id "de.undercouch.download" version "5.6.0"
}

dependencies {
    implementation project(":game-app:domain-data")
    implementation project(":game-app:game-core")
    testImplementation project(":game-app:game-headless")
    testImplementation project(":lib:java-extras")
    testImplementation project(":lib:test-common")
}

test {
    // AiGameTest is memory intensive due to ConcurrentBattleCalculator threads deserializing GameData concurrently.
    maxHeapSize = "2G"
}

// Gather all URLs in the same game list file
def downloadURLs = []
def saveGamesFile = project.layout.projectDirectory.file("save-game-list.txt").asFile
saveGamesFile.readLines().eachWithIndex { path, idx ->
    if (!path.startsWith("#")) {
        downloadURLs.add(path)
    }
}

// Create a download task for the URLs
def downloadTask = tasks.register("downloadSaveGames", de.undercouch.gradle.tasks.download.Download) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = "Downloads save game files"

    src downloadURLs
    dest project.layout.buildDirectory.dir("downloads/save-games")
    overwrite false
    onlyIfModified true
    quiet true
}

// And wire test resource processing as a dependant on the download task
tasks.named("processTestResources", ProcessResources).configure {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(downloadTask) {
        into "save-games"
    }
}
