plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

archivesBaseName = "$group-$name"
description = 'TripleA DropWizard Http Server'
mainClassName = 'org.triplea.server.http.ServerApplication'
ext {
    releasesDir = file("$buildDir/releases")
}


configurations {
    testImplementation {
        // database-rider brings in slf4j-simple as a transitive dependency
        // DropWizard has logback baked in and cannot have multiple slf4j bindings.
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
}

dependencies {
    implementation project(':java-extras')
    implementation project(':http-clients')
    implementation project(':lobby-db-dao')

    implementation "com.sun.mail:jakarta.mail:$jakartaMailVersion"
    implementation "io.dropwizard:dropwizard-core:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-jdbi3:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-auth:$dropwizardVersion"
    implementation 'org.jdbi:jdbi3-core:3.8.2'
    implementation 'org.jdbi:jdbi3-sqlobject:3.8.2'

    // https://mvnrepository.com/artifact/es.moki.ratelimitj/ratelimitj-dropwizard
    compile group: 'es.moki.ratelimitj', name: 'ratelimitj-dropwizard', version: '0.6.0'
// https://mvnrepository.com/artifact/es.moki.ratelimitj/ratelimitj-redis
// https://mvnrepository.com/artifact/es.moki.ratelimitj/ratelimitj-inmemory
    compile group: 'es.moki.ratelimitj', name: 'ratelimitj-inmemory', version: '0.6.0'
// https://mvnrepository.com/artifact/es.moki.ratelimitj/ratelimitj-core
//    compile group: 'es.moki.ratelimitj', name: 'ratelimitj-core', version: '0.6.0'


    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.2'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0'
    implementation 'javax.activation:activation:1.1.1'
    implementation "org.mindrot:jbcrypt:$jbcryptVersion"

    runtimeOnly "org.postgresql:postgresql:$postgresqlVersion"

    testImplementation project(':test-common')
    testImplementation 'com.github.database-rider:rider-junit5:1.5.2'
    testImplementation 'io.dropwizard:dropwizard-testing:1.3.12'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task portableInstaller(type: Zip, group: 'release', dependsOn: shadowJar) {
    from file('configuration-prerelease.yml')
    from file('configuration-production.yml')

    from(shadowJar.outputs) {
        into 'bin'
    }
}

task release(group: 'release', dependsOn: portableInstaller) {
    doLast {
        publishArtifacts(portableInstaller.outputs.files)
    }
}
