plugins {
    id "java"
    id "pmd"
    id "eclipse"
    alias(libs.plugins.gradle.versions)
    alias(libs.plugins.spotless) apply false
}

ext {
    schemasDir = file("config/triplea/schemas")
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

allprojects {
    apply plugin: "com.diffplug.spotless"
    apply plugin: "java"

    spotless {
        format "allFiles", {
            target "*"
            targetExclude "gradlew.bat"
            endWithNewline()
            leadingTabsToSpaces()
            trimTrailingWhitespace()
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

subprojects {
    apply plugin: "checkstyle"
    apply plugin: "jacoco"
    apply plugin: "java"
    apply plugin: "pmd"

    apply from: rootProject.file("gradle/scripts/version.gradle")

    group = "triplea"

    dependencies {
        implementation(libs.jlayer) {
            exclude group: 'junit', module: 'junit'
        }
        compileOnly(libs.lombok)
        annotationProcessor(libs.lombok)

        testCompileOnly(libs.lombok)
        testAnnotationProcessor(libs.lombok)

        implementation libs.logback.classic
        implementation libs.caffeine
        implementation libs.gson
        implementation libs.guava
        implementation libs.dropwizard.websockets
        implementation libs.jakarta.mail
        implementation libs.jaxb.impl
        implementation libs.commons.io
        implementation libs.feign.gson
        implementation libs.commons.math3
        implementation libs.commons.text
        implementation libs.apache.httpmime
        implementation libs.java.websocket
        implementation libs.jetbrains.annotations
        implementation libs.xchart
        implementation libs.radiance.substance
        implementation libs.snakeyaml.engine
        testImplementation libs.jackson.datatype.jsr310
        testImplementation libs.hamcrest.optional
        testImplementation libs.wiremock
        testImplementation libs.equals.verifier
        testImplementation libs.assertj.core
        testImplementation libs.awaitility
        testImplementation libs.hamcrest
        testImplementation libs.bundles.junit
        testImplementation libs.bundles.mockito
        testImplementation libs.sonatype.goodies.prefs
        testImplementation libs.bundles.xmlunit
        testImplementation libs.wiremock.junit5
        testRuntimeOnly libs.junit.jupiter.engine
        testRuntimeOnly libs.junit.platform.launcher
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += [
                "-Xlint:none,-processing"
        ]
        options.encoding = "UTF-8"
        options.incremental = true
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform() {}
        testLogging {
            exceptionFormat = "full"
            events = ["standardOut", "standardError", "skipped", "failed"]
        }
    }

    checkstyle {
        toolVersion = libs.versions.checkstyle.get()
        configFile = rootProject.file(".build/checkstyle.xml")
        configProperties = [samedir: configFile.parent]
    }

    checkstyleMain {
        maxWarnings = 0
        source sourceSets.main.output.resourcesDir
    }

    checkstyleTest {
        maxWarnings = 0
        source sourceSets.test.output.resourcesDir
        exclude "**/map-xmls/*.xml"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            xml.outputLocation = project.layout.buildDir.file("jacoco.xml")
            html.required = true
        }
    }

    pmd {
        consoleOutput = true
        ruleSetFiles = files(rootProject.file(".build/pmd.xml"))
        ruleSets = []
        incrementalAnalysis = true
        toolVersion = libs.versions.pmd.get()
    }

    spotless {
        java {
            googleJavaFormat()
            removeUnusedImports()
        }
    }
}
