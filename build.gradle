plugins {
    id 'java'
    id 'com.github.ben-manes.versions' version '0.44.0'
    id 'io.franzbecker.gradle-lombok' version '5.0.0' apply false
    id 'com.diffplug.spotless' version '6.11.0' apply false
}

apply plugin: 'eclipse'

ext {
    schemasDir = file('config/triplea/schemas')
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

allprojects {
    apply plugin: 'com.diffplug.spotless'

    spotless {
        format 'allFiles', {
            target '*'

            endWithNewline()
            indentWithSpaces()
            trimTrailingWhitespace()
        }
    }
}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'pmd'
    apply plugin: 'io.franzbecker.gradle-lombok'

    apply from: rootProject.file('gradle/scripts/release.gradle')
    apply from: rootProject.file('gradle/scripts/version.gradle')

    group = 'triplea'
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    ext {
        apacheHttpComponentsVersion = '4.5.13'
        awaitilityVersion = '4.2.0'
        bcryptVersion = '0.9.0'
        caffeineVersion = '3.1.1'
        checkstyleVersion = '8.45'
        commonsCliVersion = '1.5.0'
        commonsCodecVersion = '1.15'
        commonsIoVersion = '2.11.0'
        commonsMathVersion = '3.6.1'
        commonsTextVersion = '1.10.0'
        databaseRiderVersion = '1.35.0'
        dropwizardVersion = '2.1.0'
        dropwizardWebsocketsVersion = '1.3.14'
        equalsVerifierVersion = '3.11'
        feignCoreVersion = '12.0'
        feignGsonVersion = '12.0'
        javaWebSocketVersion = '1.5.3'
        gsonVersion = '2.10'
        guavaVersion = '31.1-jre'
        hamcrestJsonVersion = '0.2'
        hamcrestOptionalVersion = '2.0.0'
        hamcrestVersion = '2.0.0.0'
        jacksonDataTypeVersion = '2.14.0'
        jakartaMailVersion = '2.0.1'
        javaWebsocketVersion = '1.5.3'
        javaxActivationVersion = '1.1.1'
        jaxbApiVersion = '2.3.1'
        jaxbCoreVersion = '4.0.1'
        jaxbImplVersion = '4.0.1'
        jdbiVersion = '3.34.0'
        jetbrainsAnnotationsVersion = '23.0.0'
        jlayerVersion = '1.0.1.4'
        junitJupiterVersion = '5.9.1'
        junitPlatformLauncherVersion = '1.9.1'
        logbackClassicVersion = '1.4.5'
        mockitoVersion = '4.9.0'
        openFeignVersion = '12.0'
        postgresqlVersion = '42.5.0'
        snakeYamlVersion = '2.5'
        sonatypeGoodiesPrefsVersion = '2.3.4'
        substanceVersion = '4.5.0'
        wireMockJunit5Version = '1.3.1'
        wireMockVersion = '2.27.2'
        xchartVersion = '3.8.2'
        xmlUnitCore = '2.9.0'
        xmlUnitMatchers = '2.9.0'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation "ch.qos.logback:logback-classic:$logbackClassicVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        testImplementation "com.github.npathai:hamcrest-optional:$hamcrestOptionalVersion"
        testImplementation "nl.jqno.equalsverifier:equalsverifier:$equalsVerifierVersion"
        testImplementation "org.hamcrest:java-hamcrest:$hamcrestVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitPlatformLauncherVersion"
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += [
            '-Xlint:none,-processing'
        ]
        options.encoding = 'UTF-8'
        options.incremental = true
    }

    tasks.withType(Test).configureEach {
        testLogging {
            exceptionFormat = 'full'
            showStandardStreams = true
        }
    }

    // Add a summary of failed test case to the end of execution
    //   https://stackoverflow.com/questions/43917709/how-do-i-show-a-list-of-all-the-test-failures-after-completion-of-a-gradle-task
    // add a collection to track failedTests
    ext.failedTests = []

    // add a testlistener to all tasks of type Test
    tasks.withType(Test) {
        afterTest { TestDescriptor descriptor, TestResult result ->
            if(result.resultType == TestResult.ResultType.FAILURE){
                failedTests << ["${descriptor.className}::${descriptor.name}"]
            }
        }
    }

    // print out tracked failed tests when the build has finished
    gradle.buildFinished {
        if(!failedTests.empty){
            println "Failed tests for ${project.name}:"
            failedTests.each { failedTest ->
                println failedTest
            }
            println ""
        }
    }

    checkstyle {
        toolVersion = "$checkstyleVersion"
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        configProperties = [samedir: configFile.parent]
    }

    checkstyleMain {
        maxWarnings = 0
        source sourceSets.main.output.resourcesDir
    }

    checkstyleTest {
        maxWarnings = 0
        source sourceSets.test.output.resourcesDir
        exclude '**/map-xmls/*.xml'
    }

    jacocoTestReport {
        reports {
            xml.required = true
            xml.destination file("${buildDir}/jacoco.xml")
            html.required = true
        }
    }

    lombok {
        version = '1.18.20'
        sha256 = 'ce947be6c2fbe759fbbe8ef3b42b6825f814c98c8853f1013f2d9630cedf74b0'
    }

    pmd {
        consoleOutput = true
        ruleSetFiles = files(rootProject.file('config/pmd/pmd.xml'))
        ruleSets = []
        incrementalAnalysis = true
    }

    spotless {
        java {
            googleJavaFormat()
            removeUnusedImports()
        }
    }

    test {
        useJUnitPlatform()
    }
}
