plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

group = 'triplea'

version = System.getenv("TRAVIS_TAG") ?: ""

description = 'TripleA is a free online turn based strategy game and board game engine, similar to such board games as Axis & Allies or Risk.'
mainClassName = "games.strategy.engine.framework.GameRunner"

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.6
targetCompatibility = 1.6
tasks.withType(JavaCompile) { options.incremental = true }

sourceSets {
    main {
        java { srcDir 'src' }
        resources { srcDir 'data' }
    }
    test {
        java { srcDirs 'test' }
    }
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName, 'TripleA-Version': version
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'javax.activation:activation:1.1.1'
    compile 'commons-codec:commons-codec:1.9'
    compile 'commons-httpclient:commons-httpclient:3.1'
    compile 'commons-logging:commons-logging:1.1.3'

    compile 'com.github.insubstantial:flamingo:7.3'
    compile 'com.github.insubstantial:laf-plugin:7.3'
    compile 'com.github.insubstantial:laf-widget:7.3'
    compile 'com.github.insubstantial:substance:7.3'
    compile 'com.github.insubstantial:substance-swingx:7.3'
    compile 'com.github.insubstantial:substance-flamingo:7.3'
    compile 'com.github.insubstantial:trident:7.3'

    compile 'org.swinglabs.swingx:swingx-all:1.6.4'
    compile 'com.sun.mail:mailapi:1.5.1'
    compile 'com.sun.mail:smtp:1.5.1'
    compile files('lib/upnp.jar')
    compile files('lib/AppleJavaExtensions-1.6.jar')

    testCompile 'org.apache.derby:derby:10.10.1.1'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper, group: 'gradle') {
    distributionUrl = "http://services.gradle.org/distributions/gradle-2.5-all.zip"
}

task copyShadowSetup(dependsOn: shadowJar) << {
   def binDir = new File( "bin")
   binDir.mkdir()
}

String zipBase = group + '-' + version
String zipDestFile = zipBase + '.zip'

task copyShadow(dependsOn: copyShadowSetup, type: Copy) {
   from 'build/libs/' + zipBase + '-all.jar'
   into 'bin/' + group + '.jar' 
}



task zip(dependsOn: copyShadow) << {
  ant.zip(destfile: zipDestFile, compress: true) {
    zipfileset(dir: 'doc', prefix: zipBase + '/doc')
    zipfileset(dir: 'license', prefix: zipBase + '/license')
    zipfileset(dir: 'icons', prefix: zipBase + '/icons') {
       include(name: '**/*.png')
       include(name: '**/*.bmp')
       include(name: '**/*.ico')
    }
    zipfileset(dir: 'assets', prefix: zipBase + '/assets')
    zipfileset(dir: 'maps', prefix: zipBase + '/maps')
    zipfileset(dir: 'old', prefix: zipBase + '/old')
    zipfileset(dir: '.', prefix: zipBase + '/') {
       include(name: 'readme.html')
       include(name: 'system.ini')
       include(name: 'scripts/*.bat')
       include(name: 'triplea.exe')
       include(name: 'changelog.txt')
       include(name: 'TripleA_RuleBook.pdf')
       include(name: 'MacOS_users_read_this_first.txt')
    }
    zipfileset(dir: 'bin', prefix: zipBase + "/bin", filemode: '755') {
       include(name: 'triplea.jar')
    }
  }
}

def artifactsDir = new File( "artifacts")

task copyZip(dependsOn: ['zip', 'setupArtifactsDir'], type: Copy) {
  from zipDestFile
  into artifactsDir
}


task setupArtifactsDir << {
   artifactsDir.mkdir()
}

task copyShadowJar(dependsOn: 'shadowJar', type: Copy) {
   from 'build/libs/' + zipBase + '-all.jar'
   into artifactsDir
}


task releaseArtifacts( dependsOn: ['setupArtifactsDir', 'copyZip', 'copyShadowJar']) 

