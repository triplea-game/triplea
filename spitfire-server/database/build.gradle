import org.flywaydb.gradle.task.*

buildscript {
    dependencies {
        classpath "org.postgresql:postgresql:$postgresqlVersion"
    }
}

plugins {
    id 'org.flywaydb.flyway' version '9.22.0'
}

flyway {
    driver = 'org.postgresql.Driver'

}

task flywayMigrateLobbyDb(type: FlywayMigrateTask) {
    url= 'jdbc:postgresql://localhost:5432/lobby_db'
    user = 'lobby_user'
    password = 'lobby'
    locations = ['filesystem:src/main/resources/db/migration/lobby_db']
}

task flywayMigrateErrorReportDb(type: FlywayMigrateTask) {
    url= 'jdbc:postgresql://localhost:5432/error_report'
    user = 'error_report_user'
    password = 'error_report'
    locations = ['filesystem:src/main/resources/db/migration/error_report']
}

task portableInstaller(type: Zip, group: 'release') {
    from 'src/main/resources/db/migration/'
    archiveFileName = 'migrations.zip'
}

task release(group: 'release', dependsOn: portableInstaller) {
    doLast {
        publishArtifacts(portableInstaller.outputs.files)
    }
}
