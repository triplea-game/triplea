plugins {
    id "application"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.avast.gradle.docker-compose" version "0.17.6"
}

archivesBaseName = "$group-$name"
mainClassName = "org.triplea.spitfire.server.SpitfireServerApplication"
ext {
    releasesDir = file("$buildDir/releases")
}

jar {
    manifest {
        attributes "Main-Class": mainClassName
    }
}

task portableInstaller(type: Zip, group: "release", dependsOn: shadowJar) {
    from file("configuration.yml")

    from(shadowJar.outputs) {
        into "bin"
    }
}

task release(group: "release", dependsOn: portableInstaller) {
    doLast {
        publishArtifacts(portableInstaller.outputs.files)
    }
}

shadowJar {
    archiveClassifier.set ""
    // mergeServiceFiles is needed by dropwizard
    // Without this configuration parsing breaks and is unable to find connector type "http" for
    // the following YAML snippet:  server: {applicationConnectors: [{type: http, port: 8080}]
    mergeServiceFiles()
}

configurations {
    testImplementation {
        // database-rider brings in slf4j-simple as a transitive dependency
        // DropWizard has logback baked in and cannot have multiple slf4j bindings.
        exclude group: "org.slf4j", module: "slf4j-simple"
    }
}

dependencies {
    implementation "com.liveperson:dropwizard-websockets:$dropwizardWebsocketsVersion"
    implementation "io.dropwizard:dropwizard-auth:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-core:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-jdbi3:$dropwizardVersion"
    implementation project(":game-app:domain-data")
    implementation project(":http-clients:github-client")
    implementation project(":http-clients:lobby-client")
    implementation project(":lib:feign-common")
    implementation project(":lib:java-extras")
    implementation project(":lib:websocket-client")
    implementation project(":lib:websocket-server")
    implementation project(":servers:server-lib")
    implementation project(":spitfire-server:latest-version-module")
    implementation project(":spitfire-server:lobby-module")
    testImplementation "com.github.database-rider:rider-junit5:$databaseRiderVersion"
    testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
    testImplementation "io.github.openfeign:feign-core:$feignCoreVersion"
    testImplementation "io.github.openfeign:feign-gson:$feignGsonVersion"
    testImplementation "org.awaitility:awaitility:$awaitilityVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testImplementation project(":spitfire-server:database-test-support")
    testImplementation project(":lib:test-common")
    testImplementation "org.java-websocket:Java-WebSocket:$javaWebSocketVersion"
    runtimeOnly "org.postgresql:postgresql:$postgresqlVersion"
}


dockerCompose {
    lobby {
        projectName = "triplea"
        useComposeFiles = ["../../docker-compose.yml"]
        startedServices = ["lobby"]
        captureContainersOutput = true
    }
}

sourceSets {
    integ {
        java.srcDir("$projectDir/src/integ/java")
        resources.srcDir("$projectDir/src/integ/resources")

        annotationProcessorPath += main.annotationProcessorPath
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integImplementation.extendsFrom testImplementation
    integRuntime.extendsFrom testRuntime
}

task integTest(type: Test) {
    doFirst {
        dockerCompose.lobby.exposeAsEnvironment(integTest)
    }
    testClassesDirs = sourceSets.integ.output.classesDirs
    classpath = sourceSets.integ.runtimeClasspath
    dependsOn("shadowJar")
    dependsOn("lobbyComposeUp")
}
integTest.finalizedBy("lobbyComposeDown")
check.dependsOn(integTest)
