plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'de.undercouch.download' version '5.0.1'
}

archivesBaseName = "$group-$name"
description = 'TripleA Smoke Testing Application'
mainClassName = 'org.triplea.test.smoke.ClientConnect'

dependencies {
    implementation project(':game-app:game-core')
    implementation project(':http-clients:feign-common')
    implementation project(':http-clients:lobby-client')
    testImplementation project(':lib:java-extras')
    testImplementation project(':lib:test-common')
}

shadowJar {
    archiveClassifier.set ''
}

tasks.test.dependsOn("downloadXmls", "downloadSaveGames")

task downloadXmls {
    doLast {
        mkdir "$projectDir/src/test/resources/map-xmls/"
        file('map-list.txt').eachLine { path ->
            if (!path.startsWith('#')) {
                download.run {
                    src "$path"
                    dest "$projectDir/src/test/resources/map-xmls/"
                    overwrite false
                }
            }
        }
    }
}

task downloadSaveGames {
    doLast {
        mkdir "$projectDir/src/test/resources/save-games/"
        file('save-game-list.txt').eachLine { path ->
            if (!path.startsWith('#')) {
                download.run {
                    src "$path"
                    dest "$projectDir/src/test/resources/save-games/"
                    overwrite false
                }
            }
        }
    }
}

clean.doFirst {
    delete "$projectDir/src/test/resources/map-xmls/"
    delete "$projectDir/src/test/resources/save-games/"
}
